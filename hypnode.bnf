<program> ::= <definition_list>

<definition_list> ::= <definition_list> <node_definition> 
                | <definition_list> <type_definition> 
                | // Note this is empty

<type_definition> ::= <type_declaration> "=" <type_implementation>

<type_declaration> ::= "type" <identifier>

<type_implementation> ::= "{" <field_definition_list> "}" 
                | <type>

<field_definition_list> ::= <field_definition_list> <field_definition> 
                | // Note this is empty

<field_definition> ::= <identifier> ":" <type_implementation> ";"

<node_definition> ::= <node_attribute_list> <node_declaration> "=" <node_implementation>

<node_declaration> ::= "node" <identifier> "(" <port_definition_list> ")" "=>" "(" <port_definition_list> ")"

<node_attribute_list> ::= <node_attribute_list> <node_attribute> 
                | // Note this is empty

<port_definition_list> ::= <port_definition_list> <port_definition> 
                | // Note this is empty

<port_definition> ::=  <port_attribute_list> <identifier> ":" <type_implementation> ","

<port_attribute> ::= "@optional" 
                | "@required" 
                | "@trigger" <identifier>

<port_attribute_list> ::= <port_attribute_list> <port_attribute> 
                | // Note this is empty

<node_attribute> ::= "@export" <string_literal>

<node_implementation> ::= "{" <statement_definition_list> "}" 
                | "@import" <string_literal>

<statement_definition_list> ::= <statement_definition_list> <statement_definition> 
                | // Note this is empty

<statement_definition> ::= <node_instance> 
                | <node_connection>

<node_instance> ::= "let" <identifier> ":" <identifier> ";"

<node_connection> ::= <field_access> "<-" <field_access> ";"

<field_access> ::= <identifier> <identifier_accesss_list>

<identifier_accesss_list> ::= <identifier_accesss_list> "." <identifier>
                | // Note this is empty

<type> ::= <primitive_type> 
                | <identifier>

<primitive_type> ::= "i8"
                | "i16"
                | "i32"
                | "i64"
                | "u8"
                | "u16"
                | "u32"
                | "u64"
                | "f32"
                | "f64"
                | "string"
                | "bool"

<identifier> ::= // Any valid UTF-8 symbol combination except keywords and literals

<string_literal> ::= // Todo regex

<number_literal> ::= // Todo regex

<character_literal> ::= // Todo regex

<bool_literal> ::= "true" 
                | "false"